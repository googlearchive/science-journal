apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

apply from: 'version.gradle'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    publishNonDefault true

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode buildVersionCode()
        versionName buildVersionName()
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        absolutePaths false

        lintConfig file("lint.xml")
        warning "MissingTranslation", "ExtraTranslation"

        // b/64679950
        warning "InvalidPackage"

        htmlOutput file("build/reports/whistlepunk-lint-results.html")
        xmlOutput file("build/reports/whistlepunk-lint-results.xml")
    }

    sourceSets {
        main {
            java {
                srcDir '../../third_party/jsyn'
                srcDir 'src/api/wireapi/java'
            }
            proto {
                srcDir 'src/main/proto'
            }
            aidl {
                srcDir 'src/api/wireapi/aidl'
            }
        }
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
            java.srcDir 'src/robolectricTest/java'
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
    generateProtoTasks {
        all()*.builtins {
            javanano {
                // Options added to --javanano_out
                option 'java_multiple_files=false'
                option 'ignore_services=true'
            }
        }
    }
}

dependencies {
    compile project(':scalarinputapi')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3.1'
    compile 'com.android.support:support-annotations:26.0.2'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:percent:25.3.1'
    compile 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    compile 'com.google.guava:guava:20.0'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'joda-time:joda-time:2.9.4'
    compile 'org.joda:joda-convert:1.8'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testCompile 'junit:junit:4.12'
    androidTestCompile('junit:junit:4.12') {
        exclude module: 'hamcrest-core'
    }
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    compile 'com.google.dagger:dagger:2.11'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.bignerdranch.android:expandablerecyclerview:2.1.1'
    testCompile('org.robolectric:robolectric:3.3.2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile 'org.robolectric:shadows-core:3.3.2'
}
